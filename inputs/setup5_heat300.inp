*  Periodic boundary METHOD
*  heating from 200 to 300 K
*  QM-MM partition
*

BOMLEV 0

! Set parameters
! --------------
set TOPPAR  = /home/sma/kinase/toppar               ! topology and parameters directory
set top     = @TOPPAR/top_all36_prot.rtf            ! Protein topology
set par     = @TOPPAR/par_all36_prot.prm            ! Protein parameters
set watstr  = @TOPPAR/toppar_water_ions.str         ! water and ions
set modstr  = @TOPPAR/toppar_all36_prot_model.str   ! model compound parameters

! Read topology and parameter files
! ---------------------------------
open read card unit 10 name @top
read  rtf card unit 10
close unit 10

open read card unit 10 name @par
read param card flex unit 10
close unit 10

! Read top/par file for model compounds
! -------------------------------------------------
stream @modstr

! Read water top file
! -------------------
stream @watstr

!Read PSF file
!----------------
open read form unit 10 name setup4.psf
read psf card unit 10
close unit 10

! Read in coordinate file
!------------------------------
open read form unit 13 name setup5_heat200.crd
read coord card unit 13
close unit 13

! Define selections
! -----------------
define prot sele segid 4G5J end
define qms sele segid QMP  end

define solu sele prot .or. qms end

define solv sele segid CWAT .or. segid SOLV .or. segid SOD end

!define water   sele resn TIP3 end
!define heavy   sele protein .and. ( .not. hydrogen ) end

! Set-up image information for cubic periodic boundaries
!-----------------------------------------------------------
set 6 85.0000
set 7 77.0000
set 8 66.0000

!define the crystal lattice type and geometry
!-------------------------------------------------
crystal define ORTH   @6 @7 @8  90.0  90.0  90.0

! build the crystal along with the images within the cutoff
!------------------------------------------------------------
crystal build Noper 0 cutoff 14.0

! Only Recenter water and counter ions
!--------------------------------------
image byres xcen 0.0 ycen 0.0 zcen 0.0 select solv end

update elec group switch Ewald PMEwald Kappa 0.34 -
       ORDEr 6 FFTX 72 FFTY 72 FFTZ 60 QCOR 0 -
       vdw vgroup vswitch -
       cutnb 14.0 ctofnb 13.0 ctonnb 12.0 WMIN 1.2  -
       inbf 25 imgfrq 25  cutim 14.0

quantum group sele qms end glnk sele bynu 5225 show end - !
        REMO pdp3 charge 0.0 scfc 0.000001 SWITched -!NOGAussian
        NEWD 1 KMAX 5

shake bonh para tol 1.0e-6 sele all end -
   sele  (.not. qms .and. hydrogen) end

! Read restart file
!----------------------------------------------
open read formatted unit 9 name setup5_heat200.rst

! Write restart file
!------------------------
open write form unit 10 name setup5_heat300.rst

! Write trajectory file
!------------------------
!open write unfo unit 11 name setup5_heat300.trj

! Write velocity trajectory file
!---------------------------------
!open write form unit 16 name setup5_heat300.nvt

dyna cpt rest nstep 5000 timestep 0.001 iseed 758678 -
    iprfrq 5000 ihtfrq 10 ieqfrq 100  -
    iuncrd -11 iunrea 9 iunwri 10 iunvel -1 kunit -1 -
    nprint 100 nsavc 0 nsavv 0 ihbfrq 0 -
    iasors 1 iasvel 1 iscvel 0 ichecw 0 -
    twindh 10.0 twindl -10.0 ntrfrq 500 -
    pcons pint pmass 500.0 pref 1.0 pgamma 25.0 -
    prefi 1.0 preff 1.0 -
    hoover reft 298.15 tmass 2000.0 tbath 298.15 -
    firstt 200.00 finalt 298.15 tstruc 298.15

! Write new coordinate file
!----------------------------
open write form unit 15 name setup5_heat300.crd
write coord card unit 15

! Write new PDB file
!---------------------
open write form unit 15 name setup5_heat300.pdb
write coord pdb unit 15               

scalar x statistics sele solu end
scalar y statistics sele solu end
scalar z statistics sele solu end

stop 
